
float = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
int = @{ ASCII_DIGIT+ }
number = _{ float | int }

strchar = { !"\"" ~ !"$" ~ !"\\" ~ ANY }
stresc = @{ "\\n" | "\\\"" | "\\" }
strlit = @{ strchar+ }
strid = _{ "$" ~ id }
strx = _{ "${" ~ expr ~ "}" }
str = ${ "\"" ~ (stresc | strlit | strid | strx )* ~ "\"" }

negative = { "-" }
plus = { "+" }
dash = { "-" }
star = { "*" }
slash = { "/" }
dot = { "." }
question = { "?" }
modulo = { "modulo" }
and = { "and" }
or = { "or" }
not = { "not" }
less_than = { "<=" | "<" }
equality = { "==" | "!=" }
greater_than = { ">=" | ">" }
add_newline = { "\\n" }
underscore = { "_" }
block_arrow = _{ "->" | ">>" }
double_dash = _{ "--" }

prefix_op = _{
    negative
    | not
    | star
}

postfix_op = _{
    (as1 ~ add_newline)
    | tuple
}

// keywords
func_mode = { "func" | "macro" }
let_mode = { "let" | "const" }
interface_block = { double_dash }
rust_block = { "-RUST-" }
mxport = { "import" | "export" }

anyspace_infix_op = _{
    modulo
    | equality | greater_than
    | and | or
    | dot
}

hspace_infix_op = _{
    dash | plus | slash | star | less_than
}

spaced_infix_op = _{
    as0 ~ anyspace_infix_op ~ as0
    | hs0 ~ hspace_infix_op ~ as0
}

type_postfix_op = _{
    question
    | star
}

type_infix_op = _{
    dot
}

keywords = _{
    func_mode
    | mxport
    | "datatype"
    | "match" | "if"
    | and | or | not
}

keyword_prefix = _{
    keywords ~ ( LETTER | NUMBER | "_" )+
}

// horizontal space only
hs0 = _{ " "* }
hs1 = _{ " "+ }
// any horizontal or vertical space
as0 = _{ (" " | NEWLINE)* }
as1 = _{ (" " | NEWLINE)+ }
// vertical space required
vs1 = _{ NEWLINE ~ as0 }

modname_tail = _{ ASCII_ALPHA_LOWER | ASCII_DIGIT }
modname = @{ !keywords ~ ASCII_ALPHA_LOWER ~ modname_tail* }

id = @{
    keyword_prefix
    | !keywords ~ ((LETTER | "_") ~ ( LETTER | NUMBER | "_" )* | "#")
}
def_id = _{ gen_type | id }

hashtag = @{ "#" ~ id }

term = _{ hashtag | id | number | str | tuple | list | gen_type }
pattern = _{ underscore | expr }

tx_maybe_k = { (id ~ hs0 ~ ":" ~ as0)? ~ typex }
x_maybe_k = { (id ~ hs0 ~ ":" ~ as0)? ~ expr }
txlist = _{ tx_maybe_k ~ (as1 ~ tx_maybe_k)* }
xlist = _{ x_maybe_k ~ (as0 ~ "," ~ as0 ~ x_maybe_k)* ~ as0 ~ ","? }

list = { "[]" | "[" ~ as0 ~ xlist? ~ as0 ~ "]" }
tuple = { "(" ~ as0 ~ xlist? ~ as0 ~ ")" }

blockx = { (block_arrow ~ vs1 ~ stmt_block ~ double_dash) }

case = { "|" ~ pattern ~ as1 ~ arrow_block }
ifx = { "if" ~ vs1 ~ case+ ~ double_dash }
matchargs = { "match" ~ vs1 ~ case+ ~ double_dash }
matchx = { "match" ~ hs1 ~ expr? ~ vs1 ~ case+ ~ double_dash }

prefix1 = _{ prefix_op ~ as0 ~ term }
postfix1 = _{ term ~ postfix_op }
x1 = _{
    prefix1
    | postfix1
    | term
}

x2 = _{
    spaced_infix_op ~ x1
    | postfix_op
}

expr = !{
    blockx
    | x1 ~ x2+
    | x1
}

compound_expr = _{
    ifx
    | matchx
    | matchargs
    | expr
}

// types

list_type = { "[" ~ typex ~ "]" }
tuple_type = { "(" ~ txlist? ~ ")" }
gen_type = { "<" ~ typex ~ as1 ~ txlist ~ ">" }

type_term = _{
    tuple_type
    | list_type
    | gen_type
    | id
}

type_func = {
    "::" ~ as1 ~ tx_maybe_k*
    | tx1 ~ as1 ~ "::" ~ (as1 ~ tx_maybe_k)*
}

tx1 = _{
    type_term ~ type_postfix_op
    | type_term
}

tx2 = _{
    type_infix_op ~ tx1
    | type_postfix_op
}

typex = { tx1 ~ tx2* }

any_type = { type_func | typex }

// statements

def_func_args = { (hs0 ~ "::" ~ (as0 ~ tx_maybe_k)+)? }
def_func_result = { (as0 ~ ":" ~ typex)? }
def_func = !{
    func_mode ~ as1 ~ def_id ~ def_func_result ~ def_func_args ~ as1 ~ func_block
}

def_struct_args = _{ def_id ~ def_func_args }
def_struct = !{ "datatype" ~ hs1 ~ def_struct_args ~ as1 ~ "--" }

def_enum_variant = { vs1 ~ "|" ~ ( def_struct_args | def_id ) }
def_enum = !{ "datatype" ~ hs1 ~ def_id ~ def_enum_variant+ ~ vs1 ~ "--" }

def_interface = !{ "interface" ~ hs1 ~ def_id ~ as1 ~ impl_block  }
def_impl = !{
    "impl" ~ hs1 ~ def_id ~ (as1 ~ "as" ~ as1 ~ def_id)? ~ as1 ~ impl_block
}

def_rust_type = !{ "datatype" ~ hs1 ~ id ~ as1 ~ rust_block }

let_stmt = !{ let_mode ~ hs1 ~ id ~ as1 ~ ":=" ~ as1 ~ compound_expr }

if_stmt = !{ "if" ~ hs1 ~ expr ~ as1 ~ arrow_block ~ double_dash }

return_stmt = !{ "return" ~ as1 ~ compound_expr }

comment_line = _{ "##" ~ (!NEWLINE ~ ANY)* }

mxid = _{ "." ~ id }
mxhead = @{
    ("/" ~ modname)
    | ("../" ~ modname)
    | modname
}
mxmod = @{ mxhead ~ ("/" ~ modname)* ~ mxid? | mxid }
mxline = { mxmod ~ (hs1 ~ mxblock)? }
mxblock = !{ block_arrow ~ vs1 ~ (mxline ~ vs1)+ ~ "--" }
mxstmt = !{ mxport ~ hs1 ~ (mxline | star) }

stmt = _{
    def_func
    | def_rust_type
    | def_enum
    | def_impl
    | def_interface
    | def_struct
    | let_stmt
    | if_stmt
    | return_stmt
    | mxstmt
    | comment_line
    | compound_expr
}

line = _{ hs0 ~ stmt? }
stmt_block = ${ (line ~ vs1)* }
arrow_block = _{
    block_arrow ~ (hs1 ~ expr ~ vs1 | vs1 ~ stmt_block)
}
impl_block = _{
    "::" ~ vs1 ~ stmt_block ~ double_dash
}
case_block = { case+ }
func_block = _{
    rust_block
    | interface_block
    | case_block ~ double_dash
    | (block_arrow ~ vs1 ~ stmt_block ~ double_dash)
    | (block_arrow ~ hs1 ~ expr ~ hs1 ~ double_dash)
}
file = ${ (line ~ "\n")* ~ line ~ "\n"? ~ EOI }
