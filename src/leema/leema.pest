
float = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
int = @{ ASCII_DIGIT+ }
number = _{ float | int }

strchar = { !"\"" ~ !"$" ~ ANY }
strlit = @{ strchar+ }
strid = _{ "$" ~ id }
strx = _{ "${" ~ expr ~ "}" }
str = ${ "\"" ~ (strlit | strid | strx )* ~ "\"" }

negative = { "-" }
plus = { "+" }
dash = { "-" }
star = { "*" }
slash = { "/" }
tick = { "'" }
dot = { "." }
modulo = { "modulo" }
and = { "and" }
or = { "or" }
not = { "not" }
less_than = { "<" | "<=" }
equality = { "==" | "!=" }
greater_than = { ">" | ">=" }
add_newline = { "\\n" }
block_arrow = _{ "->" | ">>" }
double_dash = _{ "--" }

prefix_op = _{
    negative
    | not
}

postfix_op = _{
    add_newline
    | call_args
}

// keywords
func_mode = { "func" | "macro" }
rust_block = { "-RUST-" }
mxport = { "import" | "export" }

infix_op = _{
    dot
    | tick
    | star | slash | modulo
    | plus | dash
    | less_than | equality | greater_than
    | and | or
}

keywords = _{
    func_mode
    | mxport
}

// horizontal space only
hs0 = _{ " "* }
hs1 = _{ " "+ }
// any horizontal or vertical space
as0 = _{ (" " | NEWLINE)* }
as1 = _{ (" " | NEWLINE)+ }
// vertical space required
vs1 = _{ NEWLINE ~ as0 }

modname_tail = _{ ASCII_ALPHA_LOWER | ASCII_DIGIT }
modname = @{ !keywords ~ ASCII_ALPHA_LOWER ~ modname_tail* }

id = @{ !keywords ~ (LETTER | "_") ~ ( LETTER | NUMBER | "_" )* }
def_id = { id ~ ("'" ~ id)* }

term = _{ id | number | str | ("(" ~ expr ~ ")") }

x_maybe_k = { (id ~ hs0 ~ ":" ~ hs0)? ~ expr }
xlist = _{ x_maybe_k ~ ("," ~ x_maybe_k)* ~ ","? }
call_args = { "(" ~ xlist? ~ ")" }

prefix1 = _{ prefix_op ~ term }
postfix1 = _{ term ~ postfix_op }
x1 = _{
    prefix1
    | postfix1
    | term
}

expr = !{
    x1 ~ (infix_op ~ x1)+
    | x1
}

// types

list_type = { "[" ~ typex ~ "]" }
generic_type = { id ~ ("'" ~ type_term)+ }

type_term = _{
    "(" ~ typex ~ ")"
    | list_type
    | generic_type
    | id
}

type_func = {
    "::" ~ x_maybe_k*
    | type_term ~ "::" ~ x_maybe_k*
    | type_term ~ "::"
}

typex = { type_func | type_term }

// statements

def_func_arg = { (id ~ ":" ~ type_term) | type_term }
def_func_args = { ("::" ~ def_func_arg+)? }
def_func_result = { (":" ~ type_term)? }
def_func = !{ func_mode ~ id ~ def_func_result ~ def_func_args ~ func_block }

datatype = !{ "datatype" ~ def_id ~ def_func_args ~ "--" }

mxid = _{ "." ~ id }
mxhead = @{
    ("/" ~ modname)
    | ("../" ~ modname)
    | modname
}
mxmod = @{ mxhead ~ ("/" ~ modname)* ~ mxid? | mxid }
mxline = { mxmod ~ (hs1 ~ mxblock)? }
mxblock = !{ block_arrow ~ vs1 ~ (mxline ~ vs1)+ ~ "--" }
mxstmt = !{ mxport ~ hs1 ~ (mxline | star) }

stmt = _{
    datatype
    | def_func
    | mxstmt
    | expr
}

line = _{ hs0 ~ stmt? }
stmt_block = ${ (line ~ vs1)* }
arrow_block = _{ block_arrow ~ stmt_block }
func_block = _{
    rust_block
    | (block_arrow ~ stmt_block ~ double_dash)
    | (block_arrow ~ expr ~ double_dash)
}
file = ${ (line ~ "\n")* ~ line ~ "\n"? ~ EOI }
