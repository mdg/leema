
import list


func assign(line: [#], col: Int, rkr: #, bb: Int, bw: Int, pcs: [#]): [#]
|(line, _, _, 0, 0, []) -> line
|(line, col, rkr, bb, bw, pcs) ->
    if
    |col == bb ->
        assign(#bishop;line, col+1, rkr, 0, bw, pcs)
    |col == bw ->
        assign(#bishop;line, col+1, rkr, bb, 0, pcs)
    |else ->
        match (rkr, pcs)
        |(#rook, #rkr;apieces) ->
            assign(#rook;line, col+1, #king, bb, bw, apieces)
        |(#king, #rkr;apieces) ->
            assign(#king;line, col+1, #rook, bb, bw, apieces)
        |(_, p;apieces) ->
            assign(p;line, col+1, rkr, bb, bw, apieces)
        --
    --
--

func piece_str(piece: #): Str
|#rook -> "R"
|#knight -> "N"
|#bishop -> "B"
|#queen -> "Q"
|#king -> "K"
|p -> fail(#unknown_piece, "$p")
--

func print_pieces(all_ppcs: [#])
|[] ->
    print("\n")
|p;ppcs ->
    print(piece_str(p))
    print_pieces(ppcs)
--

func pieces_to_str(acc: Str, all_pieces: [#]): Str
|(acc, []) -> acc
|(acc, h;t) ->
    let pstr := piece_str(h)
    pieces_to_str("$acc$pstr", t)
--

func take_pieces(outs: [#], input: [(Int, #)]): [#]
|(outs, []) -> outs
|(outs, (_, head);tail) -> take_pieces(head;outs, tail)
--

func chess960(): Str ->
    let bb := 2 * (int_abs(int_random()) mod 4) + 1
    let bw := 2 * (int_abs(int_random()) mod 4) + 2
    let pieces := [
        (int_random(), #knight),
        (int_random(), #knight),
        (int_random(), #queen),
        (int_random(), #rkr),
        (int_random(), #rkr),
        (int_random(), #rkr)
        ]
    let sorted_pieces := list::sort(pieces)
    let just_pieces := take_pieces([], sorted_pieces)
    let alignment := assign([], 1, #rook, bb, bw, just_pieces)
    pieces_to_str("", alignment)
--

func main() ->
    let backline := chess960()
    print("$backline\n")
--
