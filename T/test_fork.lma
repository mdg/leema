
import tcp
import list
import task


func open_server(): tcp::Listener ->
    tcp::listen("0.0.0.0", 4008)
--

func run_client(msg: Str): Str ->
    let c := tcp::connect("127.0.0.1", 4008)
    tcp::send(c, msg)
    tcp::recv(c)
--

func run_async_client(msg: Str): Str ->
    let result := run_client(msg)
    print("client received: $result\n")
    result
--

func run_server(server: tcp::Listener) ->
    print("leema run_server\n")
    let sock := tcp::accept(server)
    task::fork(handle_request(sock))
    run_server(server)
--

func handle_request(sock: tcp::TcpSocket) ->
    let req := tcp::recv(sock)
    print("server heard: ($req)\n")
    tcp::send(sock, "I heard you say, ($req)\n")
--

func print_result(msg: Str) ->
    print("client received: $msg\n")
--

func main() ->
    let server := open_server()
    print("server listening on socket: $server\n")
    task::detach(run_server(server))
    let msgs := ["tacos", "burritos", "pizza", "pasta", "fish", "eggs"]
    let results := list::map(msgs, run_async_client)
    list::map(results, print_result)
    0
--
