
import /hyper_client
import /hyper_server
import /chess960
import /task


func intro:Str ->
    let headline := "eat more tacos"
    let blog := "<a href='https://leema.org'>leema</a>"
    let github := "<a href='https://github.com/mdg/leema'>github</a>"
    "<html><big>$headline</big><br/>$blog $github</html>\n"
--

func web_handler:Str :: req:hyper_server.Request ->
    match (req.method, req.path)
    |("GET", "/") -> intro()
    |("GET", "/chess960") ->
        let lineup := chess960.chess960()
        "$lineup\n"
    |("POST", "/test") ->
        "this was a post\n"
    |(meth, path) ->
        "no matching pattern: $meth $path\n"
    --
--

func run_server ->
    hyper_server.run(3998, web_handler)
    sleep_forever()
--

func run_client ->
    let lineup := hyper_client.get("http://localhost:3998/chess960")
    print("client_received:$lineup\n")
    let posted := hyper_client.post("http://localhost:3998/test", "test body")
    print("client_received:$posted\n")
--

func main >>
    task.detach(run_server())
    sleep(500)
    run_client()
--
